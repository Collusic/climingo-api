plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
}

group = 'com.climingo'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

test {
    useJUnitPlatform()
    finalizedBy(jacocoTestReport)
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn(test)
    reports {
        html.required.set(true)
        xml.required.set(false)
        csv.required.set(false)
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                "**/*Application*",
                "**/config/**",
                "**/util/**",
                "**/auth/api/request/**",
                "**/auth/api/response/**",
                "**/global/auth/**",
                "**/exception/**",
                "**/domain/**",
                "**/filter/**",
                "**/message/**",
                "**/s3/**",
                "**/upload/**",
                "**/vo/**",
        ])
    }))
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.70
            }
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.1.2'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.68'
    implementation 'org.springframework.cloud:spring-cloud-commons'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot:3.0.4'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.701'
    implementation 'net.bramp.ffmpeg:ffmpeg:0.7.0'

    // 모니터링
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // querydsl
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('bootBuildImage') {
    builder = 'paketobuildpacks/builder-jammy-base:latest'
}

tasks.named('test') {
    useJUnitPlatform()
}

clean {
    delete file('src/main/generated')
}